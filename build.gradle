plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'com.intershop.gradle.javacc' version '4.0.1'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'net.emustudio'
description = 'Emulator Disassembler Generator'
version = '1.5.1'

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javacc {
    configs {
        template {
            inputFile = file('src/main/javacc/Grammar.jj')
            packageName = 'net.emustudio.edigen.parser'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }

    // these are not strictly required. The default timeouts are set to 1 minute. But Sonatype can be really slow.
    // If you get the error "java.net.SocketTimeoutException: timeout", these lines will help.
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edigen'
            from components.java
            pom {
                name = 'edigen'
                description = 'Emulator Disassembler Generator'
                url = 'https://github.com/emustudio/edigen'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 2.0'
                        url = 'https://www.gnu.org/licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'sulir'
                        name = 'Matúš Sulír'
                        url = 'https://github.com/sulir/'
                    }
                    developer {
                        id = 'vbmacher'
                        name = 'Peter Jakubčo'
                        url = 'https://github.com/vbmacher'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/emustudio/edigen.git'
                    developerConnection = 'scm:git:ssh://github.com:emustudio/edigen.git'
                    url = 'https://github.com/emustudio/edigen'
                }
            }
        }
    }
    repositories {
        maven {
            // configure publishing to a local directory for testing
            name = "local"
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

signing {
    required {
        project.gradle.taskGraph.hasTask("publish")
    }

    def signingKey = findProperty("GPG_KEY") ?: System.getenv("GPG_KEY")
    def signingPassword = findProperty("GPG_PASSWORD") ?: System.getenv("GPG_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

javadoc {
    exclude 'net/emustudio/edigen/parser/**'
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
