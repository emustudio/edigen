/*
 * Copyright (C) 2011 Matúš Sulír
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
 */
options {
    STATIC = false;
}


PARSER_BEGIN(Parser)
package edigen.parser;

import edigen.Specification;
import edigen.decoder.tree.*;
import edigen.disasm.tree.*;
import edigen.util.BitSequence;
import java.util.*;

public class Parser {
    public Specification parse() throws ParseException {
        try {
            return Start();
        } catch(TokenMgrError ex) {
            throw new ParseException(ex.getMessage());
        }
    }
}
PARSER_END(Parser)


// One-line comment
<*> SKIP :
{
    <"#" (~["\n", "\r"])*>
}

// White characters
<*> SKIP :
{
    " " | "\n" | "\r" | "\t"
}

// Operators
TOKEN :
{
    <EQUALS: "=">
  | <COMMA: ",">
  | <SEMICOLON: ";">
  | <COLON: ":">
  | <OR: "|">
  | <PART_SEPARATOR: "%%">
}

// Identifiers and literals
TOKEN :
{
    <ID: (["A"-"Z", "a"-"z", "_"])+ (["A"-"Z", "a"-"z", "_", "0"-"9"])*>
  | <STRING: "\"" (~["\"", "\n", "\r"])* "\"">
  | <HEX_NUMBER: "0x" (["0"-"9", "A"-"F", "a"-"f"])+>
  | <BIN_NUMBER: ("0" | "1")+>
}

// Decimal numbers can be located only inside parentheses
TOKEN :
{
    <LEFT_PAR: "("> : IN_PARENTHESES
}

<IN_PARENTHESES> TOKEN :
{
    <RIGHT_PAR: ")"> : DEFAULT
  | <DEC_NUMBER: (["1"-"9"])+ (["0"-"9"])*>
}

<*> TOKEN :
{
    <UNEXPECTED: ~[]>
}


// Parser grammar
Specification Start():
{
    Decoder decoder;
    Disassembler disasm;
}
{
    decoder = DecoderPart()
    <PART_SEPARATOR>
    disasm = DisassemblerPart()
    <EOF>
    { return new Specification(decoder, disasm); }
}

Decoder DecoderPart():
{
    Decoder decoder;
    Rule rule;
}
{
    { decoder = new Decoder(); }
    (
        rule = DecoderRule()
        { decoder.addChild(rule); }
        <SEMICOLON>
    )+
    { return decoder; }
}

Rule DecoderRule():
{
    List<String> names;
    List<Variant> variants;
    Rule rule;
}
{
    names = RuleNameSet()
    <EQUALS>
    variants = VariantSet()
    {
        rule = new Rule(names);
        for (Variant variant : variants)
            rule.addChild(variant);
        
        return rule;
    }
}

List<String> RuleNameSet():
{
    String name;
    List<String> names = new ArrayList<String>();
}
{
    name = RuleName()
    { names.add(name); }
    (
        <COMMA>
        name = RuleName()
        { names.add(name); }
    )*
    { return names; }
}

String RuleName():
{
    Token name;
}
{
    name = <ID>
    { return name.image; }
}

List<Variant> VariantSet():
{
    Variant variant;
    List<Variant> variants = new ArrayList<Variant>();
}
{
    variant = Variant()
    {variants.add(variant);}
    (
        <OR>
        variant = Variant()
        {variants.add(variant);}
    )*
    { return variants; }
}

Variant Variant():
{
    Variant variant = new Variant();
    String value;
}
{
    [LOOKAHEAD(2) (
        value = ReturnSubRule()
            { variant.setReturnSubrule(new Subrule(value)); }
        | value = ReturnString()
            { variant.setReturnString(value); }
    ) <COLON>]
    PatternSet(variant)
    { return variant; }
}

String ReturnSubRule():
{
    Token name;
}
{
    name = <ID>
    { return name.image; }
}

String ReturnString():
{
    Token string;
}
{
    string = <STRING>
    { return string.image.replace("\"", ""); }
}

void PatternSet(Variant variant):
{
    Subrule subrule;
    Pattern constant;
}
{
    (
        subrule = SubRule()
            { variant.addChild(subrule); }
        | constant = HexConstant()
            { variant.addChild(constant); }
        | constant = BinConstant()
            { variant.addChild(constant); }
    )+
}

Subrule SubRule():
{
    String name;
    Integer length = null;
}
{
    name = SubRuleName()
    [<LEFT_PAR>
        length = SubRuleLength()
    <RIGHT_PAR>]
    {
        if (length == null)
            return new Subrule(name);
        else
            return new Subrule(name, length);
    }
}

String SubRuleName():
{
    Token name;
}
{
    name = <ID>
    { return name.image; }
}

Integer SubRuleLength():
{
    Token length;
}
{
    length = <DEC_NUMBER>
    { return Integer.parseInt(length.image); }
}

Pattern HexConstant():
{
    Token hex;
}
{
    hex = <HEX_NUMBER>
    { return new Pattern(BitSequence.fromHexadecimal(hex.image.substring(2))); }
}

Pattern BinConstant():
{
    Token bin;
}
{
    bin = <BIN_NUMBER>
    { return new Pattern(BitSequence.fromBinary(bin.image)); }
}

Disassembler DisassemblerPart():
{
    Disassembler disasm = new Disassembler();
    Format format;
}
{
    (
        format = Format()
        { disasm.addChild(format); }
        <SEMICOLON>
    )+
    { return disasm; }
}

Format Format():
{
    Format format;
    String string;
    Value value;
}
{
    string = FormatString()
    { format = new Format(string); }
    <EQUALS>
    (
        value = Value()
        { format.addChild(value); }
    )+
    { return format; }
}

String FormatString():
{
    Token string;
}
{
    string = <STRING>
    { return string.image.replace("\"", ""); }
}

Value Value():
{
    Token name;
}
{
    name = <ID>
    { return new Value(name.image); }
}
