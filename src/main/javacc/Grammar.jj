/*
 * Copyright (C) 2011-2022 Matúš Sulír, Peter Jakubčo
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
 */
options {
    STATIC = false;
    UNICODE_INPUT=true;
}

PARSER_BEGIN(Parser)
package net.emustudio.edigen.parser;

import net.emustudio.edigen.misc.BitSequence;
import net.emustudio.edigen.nodes.*;
import java.util.*;

public class Parser {
    public Specification parse() throws ParseException {
        try {
            return Start();
        } catch(TokenMgrError ex) {
            throw new ParseException(ex.getMessage());
        }
    }
}
PARSER_END(Parser)


// One-line comment
<*> SKIP :
{
    <("#" | "//") (~["\n", "\r"])*>
}

// Multi-line C-style comment
<*> SKIP :
{
    <"/*" (~["*"])* "*" (~["*","/"] (~["*"])* "*" | "*")* "/">
}

// White characters
<*> SKIP :
{
    " " | "\n" | "\r" | "\t"
}

// Special characters
TOKEN :
{
    <EQUALS: "=">
  | <COMMA: ",">
  | <SEMICOLON: ";">
  | <COLON: ":">
  | <OR: "|">
  | <L_BRACE: "[">
  | <R_BRACE: "]">
  | <L_PAREN: "(">
  | <R_PAREN: ")">
  | <PART_SEPARATOR: "%%">
  | <ROOT: "root">
}

// Identifiers and literals
TOKEN :
{
    <ID: (["A"-"Z", "a"-"z", "_"])+ (["A"-"Z", "a"-"z", "_", "0"-"9"])*>
  | <STRING: "\"" (~["\"", "\n", "\r"])* "\"">
  | <HEX_NUMBER: "0x" (["0"-"9", "A"-"F", "a"-"f"])+>
  | <DEC_NUMBER: "(" (["1"-"9"])+ (["0"-"9"])* ")">
  | <BIN_NUMBER: ("0" | "1")+>
}

<*> TOKEN :
{
    <UNEXPECTED: ~[]>
}


// Parser grammar
Specification Start():
{
    Decoder decoder;
    Disassembler disasm;
}
{
    decoder = Decoder()
    <PART_SEPARATOR>
    disasm = Disassembler()
    <EOF>
    { return new Specification(decoder, disasm); }
}

Decoder Decoder():
{
    Decoder decoder;
    Rule rule;
    Set<String> rootRules;
}
{
    rootRules = RootRules()
    { decoder = new Decoder(rootRules); }
    (
        rule = Rule()
        { decoder.addChild(rule); }
    )+
    { return decoder; }
}

Set<String> RootRules():
{
    Token name;
    Set<String> names = new LinkedHashSet<String>();
}
{
    <ROOT>
    name = <ID>
    { names.add(name.image); }
    (
        <COMMA>
        name = <ID>
        { names.add(name.image); }
    )*
    <SEMICOLON>
    {
        return names;
    }
}

Rule Rule():
{
    Rule rule;
    List<Variant> variants;
}
{
    rule = RuleNameSet()
    <EQUALS>
    variants = VariantSet()
    <SEMICOLON>
    {
        for (Variant variant : variants)
            rule.addChild(variant);
        
        return rule;
    }
}

Rule RuleNameSet():
{
    Token name;
    List<String> names = new ArrayList<String>();
    int line;
    Rule rule;
}
{
    name = <ID>
    {
        names.add(name.image);
        line = name.beginLine;
    }
    (
        <COMMA>
        name = <ID>
        { names.add(name.image); }
    )*
    {
        rule = new Rule(names);
        rule.setLine(line);
        return rule;
    }
}

List<Variant> VariantSet():
{
    Variant variant;
    List<Variant> variants = new ArrayList<Variant>();
}
{
    variant = Variant()
    {variants.add(variant);}
    (
        <OR>
        variant = Variant()
        {variants.add(variant);}
    )*
    { return variants; }
}

Variant Variant():
{
    Variant variant = new Variant();
}
{
    ( LOOKAHEAD(2) Return(variant)
    <COLON> )?
    ( Pattern(variant) )+
    {
        if (!variant.returns())
            variant.setLine(variant.getChild(0).getLine());
        return variant;
    }
}

void Return(Variant variant):
{
    Token value;
    String string;
}
{
    value = <ID>
        { variant.setReturnSubrule(new Subrule(value.image)); }
    | value = <STRING>
        {
            string = value.image.substring(1, value.image.length() - 1);
            variant.setReturnString(string);
        }
    {
        variant.setLine(value.beginLine);
    }
}

void Pattern(Variant variant):
{
    Subrule subrule;
    Pattern constant;
}
{
    subrule = Subrule()
        { variant.addChild(subrule); }
    | constant = Constant()
        { variant.addChild(constant); }
}

Subrule Subrule():
{
    Token name;
    Pattern prePattern = null;
    Integer length = null;
    Subrule subrule;
}
{
    name = <ID>
    ( prePattern = PrePattern() )?
    ( length = SubruleLength() )?
    {
        if (length == null) {
            subrule = new Subrule(name.image, prePattern);
        } else {
            subrule = new Subrule(name.image, length, prePattern);
        }
        subrule.setLine(name.beginLine);
        return subrule;
    }
}

Pattern PrePattern():
{
    Pattern pattern;
}
{
    <L_BRACE>
    pattern = Constant()
    <R_BRACE>
    { return pattern; }
}

Integer SubruleLength():
{
    Token length;
    String numberString;
}
{
    length = <DEC_NUMBER>
    {
        numberString = length.image.substring(1, length.image.length() - 1);
        return Integer.parseInt(numberString);
    }
}

Pattern Constant():
{
    BitSequence bits;
    Token constant;
    Pattern pattern;
}
{
    ( constant = <HEX_NUMBER>
        { bits = BitSequence.fromHexadecimal(constant.image.substring(2)); }
    | constant = <BIN_NUMBER>
        { bits = BitSequence.fromBinary(constant.image); } )
    {
        pattern = new Pattern(bits);
        pattern.setLine(constant.beginLine);
        return pattern;
    }
}

Disassembler Disassembler():
{
    Disassembler disasm = new Disassembler();
    Format format;
}
{
    (
        format = Format()
        { disasm.addChild(format); }
    )+
    { return disasm; }
}

Format Format():
{
    Token string;
    Format format;
}
{
    string = <STRING>
    {
        format = new Format(string.image.replace("\"", ""));
        format.setLine(string.beginLine);
    }
    <EQUALS>
    ValueSet(format)
    <SEMICOLON>
    { return format; }
}

void ValueSet(Format format):
{
    Token ruleName;
    Value value;
    Token strategy;
    List<String> strategies = new ArrayList<String>();
}
{
    (
        ruleName = <ID>
        {
            value = new Value(ruleName.image);
            value.setLine(ruleName.beginLine);
            format.addChild(value);
        }
        (
            <L_PAREN>
            strategy = <ID> { strategies.add(strategy.image); }
            (
              <COMMA>
              strategy = <ID> { strategies.add(strategy.image); }
            )*
            <R_PAREN> { value.setStrategies(strategies); }
        )?
    )+
}
